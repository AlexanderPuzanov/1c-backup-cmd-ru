rem отключение вывода сообщений
rem @echo off
rem установка кодовой страницы
chcp 1251 >nul
rem пропуск интро
goto start
--------------------------------------
Этот пакетный файл предназначен
для автоматизации архивирования баз
файловой версии 1С Бухгалтерия
в облачный сервис Яндекс Диск
--------------------------------------
Скрипт предназначен для работы
с архиватором WinRar 5
--------------------------------------
Для синхранизации с облаком установить
https://disk.yandex.ru/download/YandexDiskSetup.exe
--------------------------------------
Не размещать скрипт в каталоге
для синхронизации с облаком!
Каталог в котором будет размещен скрипт
будет использован для создания
временных файлов в процессе работы.
--------------------------------------
Пакетный файл написан 19/01/2015
Последнее исправление внесено 23/01/2015
Автор Александр Пузанов
--------------------------------------

Этот блок содержит настройки скрипта
"Пути содержащие пробелы взять в кавычки!"
Концевые слеши в путях не ставить!
Не забуте установить свои данные!
:start
rem Путь к базам 1С Бухгалтерия
set source=D:\1C\Base
rem Путь к месту к папке синхронизации с облаком
set backup=E:\backup-1C
rem Сколько дней хранить архивы.
set NumberArchives=1
rem Пароль для архивов
set password=123

rem Рабочий блок

rem Авто определение пути к WinRar
::  ошибка если не найден
if exist "%PROGRAMFILES%\WinRAR\rar.exe" (set archive="%PROGRAMFILES%\WinRAR\rar.exe") else (if exist "%PROGRAMFILES(x86)%\WinRAR\rar.exe" (set archive="%PROGRAMFILES(x86)%\WinRAR\rar.exe") else (goto NoArchive))
rem Путь к каталогу со скриптом (автоматически)
set CurrentDisk="%~dp0"
rem Файл логов (в каталоге со скриптом).
set logfile=%CurrentDisk%\backup.log

rem обработка ошибок
rem %date% текущая дата (системная переменная) 
rem если сегодня архив уже был создан
if exist %backup%\%date%.rar (goto ExistBackup)
rem если недоступен каталог с базами
if not exist %source% (goto NoSourceDir)
rem если недоступен каталог для архивов
if not exist %backup% (goto NoBackupDir)

rem архивирование
rem аргументы командной строки для rar.exe
::  a     - архивировать все
:: -cfg-  - игнорировать файл конфигурации архиватора
::          и переменную окружения.
:: -ma    - создавать архивы RAR версии 5.0
:: -htb   - тип хеша BLAKE2
:: -m5    - метод сжатия максимальный
:: -rr10p - 10% для восстановления данных
:: -ac    - снять атрибут "архивный" у файлов
:: -ow    - сохранять информацию о правах доступа
:: -agDD.MM.YYYY - добавить к имени дату в формате
:: -ep1   - не сохранять путь к папке,
::          что бы предотвратить ошибочную перезапись баз
:: -hp    - зашифровать архив включая имена файлов
:: -k     - заблокировать архив (защита от изменений)
:: --     - больше нет аргументов
%archive% a -cfg- -ma -htb -m5 -rr10p -ac -ow -agDD.MM.YYYY -ep1 -hp%password% -k %CurrentDisk% %source% --

rem результат архивирования
rem %ErrorLevel% результат выполнения архивирования
:: возвращается rar.exe
rem если успешно приступить к перемещению архива
:: если ошибка записать в ее код в лог файл
if %ErrorLevel%==0 (goto move) else (set result="Ошибка - %ErrorLevel%")
goto log

:move
rem перемещение архива в папку для хранения
rem %date% текущая дата (системная переменная)
rem переместить архив в папку синхронизации с облаком
move %CurrentDisk%\%date%.rar %backup%
rem проверить результат перемещения и записать в лог файл
if exist %backup%\%date%.rar (set result="Задание выполнено успешно") else (set result="Ошибка копирования файла"
goto :error)
goto log

:NoArchive
set result="Программа архиватор не доступна"
goto :error

:NoSourceDir
set result="Каталог с базами не доступен"
goto :error

:NoBackupDir
set result="Каталог для архивирования не доступен"
goto :error

:ExistBackup
set result="Архив был создан ранее"
goto log

:error
set error=1

:log
rem запись логов.
rem %date% текущая дата (системная переменная)
rem %time% текущие время (системная переменная)
echo %date% >> %logfile%
echo %time% >> %logfile%
echo %result% >> %logfile%
echo ... >> %logfile%
rem копируем файл логов в папку для синхронизации
move %logfile% %backup%\backup.log /Y

rem удаляем файл архива если он остался 
::  в каталоге временных файлов
if exist %CurrentDisk%\%date%.rar (del %CurrentDisk%\%date%.rar /Q)

rem удаление старых архивов
rem если текущего архива очистку не проводить
:: защита от удаления последнего архива
rem forfiles - для каждого файла выполнять
:: /P %backup% - в каталоге для синхронизации с облаком
:: /M *.rar - если архив rar
:: /D -%NumberArchives% - с датой создания более …
:: /C "cmd /c del /q @path" - удалять без подтверждения
if exist %backup%\%date%.rar (forfiles /P %backup% /M *.rar /D -%NumberArchives% /C "cmd /c del /q @path")

rem на случай если скрипт запускался 
:: другим скриптом возвращаем исходную
:: кодовою страницу
rem если есть важные ошибки
:: меняем цвет текста на красный
:: ставим скрипт на паузу 
if %error%==1 (color 0c
echo %result%
pause
) else (chcp 866 >nul
exit)
